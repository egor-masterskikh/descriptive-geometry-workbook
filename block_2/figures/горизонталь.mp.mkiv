\startcomponent .
    \startuseMPgraphic{горизонталь}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := fig_width;
    z_max := .5 * fig_height;
    y1_max := fig_height - z_max;

    draw_axes(x_max, y1_max, z_max, 0);

    pair A[], B[];

    A.x := .7 * x_max;
    A.y := .3 * y1_max;
    A.z := .4 * z_max;

    B.x := .2 * x_max;
    B.y := .7 * y1_max;
    B.z := A.z;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);

    B1 := (-B.x, -B.y);
    B2 := (-B.x, B.z);

    pickup thin_pen;

    draw A1--A2;
    draw B1--B2;

    draw 1.2[B1, A1]--1.2[A1, B1] withpen base_pen;
    draw 1.2[B2, A2]--1.2[A2, B2] withpen base_pen;

    label.llft(btex $h_1$ etex, .5[A1, B1]);
    label.top(btex $h_2$ etex, .5[A2, B2]);

    draw (O--(B2 - A2)) shifted A1;

    mark_angle_with_label(
        B1 - A1,
        B2 - A2,
        0.34,
        A1,
        2,
        "$\beta$"
    );

    mark_angle_with_label(
        (A1 - B1) + (B2 - A2),
        A1 - B1,
        0.2,
        B1,
        3,
        "$\gamma$"
    );

    path dimension_line;

    dimension_line := draw_dimension_line(
        A1,
        B1,
        2 * (font_size + labeloffset),
        -1,
        true
    );

    draw_extension_line(
        point 0.5 of dimension_line,
        -135,
        "$н.в.\ AB$"
    );

    draw_point_circ_with_label(A1, "$A_1$", urt);
    draw_point_circ_with_label(A2, "$A_2$", top);

    draw_point_circ_with_label(B1, "$B_1$", urt);
    draw_point_circ_with_label(B2, "$B_2$", top);
    \stopuseMPgraphic
\stopcomponent