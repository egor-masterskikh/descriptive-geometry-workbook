\startcomponent .
\startuseMPgraphic{прямая лежит в плоскости}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := fig_width;
    y1_max := .5 * fig_height;
    z_max := fig_height - y1_max;

    draw_axes(x_max, y1_max, z_max, 0);

    pair A[], B[], C[], P[]_[];

    A.x := .8 * x_max;
    A.y := .15 * y1_max;
    A.z := .3 * z_max;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);

    B.x := .4 * x_max;
    B.y := .8 * y1_max;
    B.z := .85 * z_max;

    B1 := (-B.x, -B.y);
    B2 := (-B.x, B.z);

    C.x := .1 * x_max;
    C.y := .5 * y1_max;
    C.z := .1 * z_max;

    C1 := (-C.x, -C.y);
    C2 := (-C.x, C.z);

    P1_1 := .45[A1, B1];
    P1_2 = whatever[A2, B2];
    xpart P1_2 = xpart P1_1;

    P2_1 := .7[A1, C1];
    P2_2 := whatever[A2, C2];
    xpart P2_2 = xpart P2_1;

    path l[];

    l1 := 1.4[P2_1, P1_1]--1.4[P1_1, P2_1];
    l2 := 1.3[P2_2, P1_2]--1.2[P1_2, P2_2];

    draw A1--A2;
    draw B1--B2;
    draw C1--C2;
    draw P1_1--P1_2;
    draw P2_1--P2_2;

    draw A1--B1--C1--cycle withpen base_pen;
    draw A2--B2--C2--cycle withpen base_pen;
    draw l1 withpen base_pen;
    draw l2 withpen base_pen;

    draw_point_circ_with_label(A1, "$A_1$", llft);
    draw_point_circ_with_label(A2, "$A_2$", ulft);
    draw_point_circ_with_label(B1, "$B_1$", bot);
    draw_point_circ_with_label(B2, "$B_2$", top);
    draw_point_circ_with_label(C1, "$C_1$", rt);
    draw_point_circ_with_label(C2, "$C_2$", urt);
    draw_point_circ_with_label(P1_1, "$1_1$", llft);
    draw_point_circ_with_label(P1_2, "$1_2$", top);
    draw_point_circ_with_label(P2_1, "$2_1$", bot);
    draw_point_circ_with_label(P2_2, "$2_2$", top);

    label.ulft("$l_1$", point 1 of l1);
    label.top("$l_2$", point 0 of l2);
\stopuseMPgraphic
\stopcomponent