\startcomponent .
\startuseMPgraphic{диметрия:натуральная величина отрезка}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    z_max := .5 * fig_height;
    y_max := (fig_height - z_max) / (0.5 * cosd 45);
    x_max := fig_width - y_max * 0.5 * cosd 45;

    pair O, X, Y, Y[], Z;

    O := (0, 0);
    X := (-x_max, 0);
    Y1 := (0, -y_max);
    Y := Y1 transformed cabinet_persp_1;
    Z := (0, z_max);

    pickup thin_pen;

    drawarrow O--X;
    label.top("$X_{12}$", X);

    drawarrow O--Z;
    label.lft("$Z_{23}$", Z);

    draw O--Y;
    draw arrow_head rotated -90 yscaled 1.6 shifted Y1 transformed cabinet_persp_1;
    draw thelabel.llft("$Y_{13}$", O) yscaled 2 transformed cabinet_persp_1 shifted Y;

    path P[];

    P1 := (O--Y--(X + Y)--X) scaled 0.85;
    P2 := (O--X--(X + Z)--Z) scaled 0.85;

    draw P1;
    draw thelabel.urt(btex П₁ etex, O) yscaled 2 transformed cabinet_persp_1 shifted point 2 of P1;

    draw P2;
    label.lrt(btex П₂ etex, point 2 of P2);

    pair A, A[], B, B[];

    A.x := .67 * x_max;
    A.y := .5 * y_max;
    A.z := .2 * z_max;

    B.x := .25 * x_max;
    B.y := .7 * y_max;
    B.z := .6 * z_max;

    A1 := (-A.x, -A.y) transformed cabinet_persp_1;
    A2 := (-A.x, A.z);

    B1 := (-B.x, -B.y) transformed cabinet_persp_1;
    B2 := (-B.x, B.z);

    A := A1 + (A.z * up);

    B := B1 + (B.z * up);

    draw A--A1;
    draw A--A2;

    draw (A--A1) shifted (A2 - A);
    draw (A--A2) shifted (A1 - A);

    draw B--B1;
    draw B--B2;

    draw (B--B1) shifted (B2 - B);
    draw (B--B2) shifted (B1 - B);

    pickup base_pen;

    draw A1--B1;
    draw A2--B2;
    draw A--B;

    pickup thin_pen;

    pair B̃, B̃[];

    B - B̃ = B2 - B̃2 = (B - B1) - (A - A1);

    draw B̃--B̃2;

    draw A--B̃;
    draw A2--B̃2;

    path dim_line_[];
    picture delta_z_label;

    delta_z_label := thelabel.top("$\delta Z_{AB}$", O) rotated 90;

    dim_line_1 := get_dim_line(
        B, B̃,
        length(B̃ - B),
        90, 90
    );
    drawdblarrow dim_line_1;
    draw delta_z_label shifted point 0.5 of dim_line_1;

    dim_line_2 := get_dim_line(
        B2, B̃2,
        1.5 * length(B̃2 - B2),
        90, 90
    );
    drawdblarrow dim_line_2;
    draw delta_z_label shifted point 0.5 of dim_line_2;


    draw_point_circ(A1);
    draw thelabel.llft("$A_1$", O) yscaled 2 transformed cabinet_persp_1 shifted A1;
    draw_point_circ_with_label(A2, "$A_2$", ulft);
    draw_point_circ_with_label(A, "$A$", top);

    draw_point_circ(B1);
    draw thelabel.bot("$B_1$", O) yscaled 2 transformed cabinet_persp_1 shifted B1;
    draw_point_circ_with_label(B2, "$B_2$", top);
    draw_point_circ_with_label(B, "$B$", urt);

    picture B̃_label;
    B̃_label := thelabel.urt("$\tilde{B}$", B̃);
    unfill boundingbox B̃_label;
    draw B̃_label;

    label.urt("$\tilde{B}_2$", B̃2);

    path alpha_mark;
    pair point_for_alpha_label;
    picture alpha_label;

    alpha_mark := get_angle_mark(
        B̃ - A, B - A,
        A, 0.28
    );
    draw alpha_mark;

    point_for_alpha_label := get_point_for_angle_label(alpha_mark, "$\alpha$");
    alpha_label := thelabel("$\alpha$", point_for_alpha_label);

    unfill boundingbox alpha_label;
    draw alpha_label;


    draw_perp_mark(B - B̃, A - B̃, B̃);
    draw_perp_mark(B2 - B̃2, A2 - B̃2, B̃2);
\stopuseMPgraphic

\startuseMPgraphic{способ прямоугольного треугольника}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    % значения x_max и z_max берём из предыдущего рисунка
    y1_max := fig_height - z_max;

    draw_axes(x_max, y1_max, z_max, 0);

    % координаты точек A и B по осям x и z берём из предыдущего рисунка
    A.y := .2 * y1_max;
    B.y := .4 * y1_max;

    A1 := (-A.x, -A.y);
    B1 := (-B.x, -B.y);

    pair B̄;
    (B̄ - B1) = unitvector(A1 - B1) rotated 90 scaled length(B2 - B̃2);

    draw A1--A2;
    draw B1--B2;

    draw A1--B1 withpen base_pen;
    draw A2--B2 withpen base_pen;

    draw A2--B̃2;
    draw B1--B̄;
    draw A1--B̄ withpen base_pen;

    draw_perp_mark(B2 - B̃2, A2 - B̃2, B̃2);
    draw_perp_mark(A1 - B1, B̄ - B1, B1);
    draw get_angle_mark_and_draw_label(
        B̄ - A1, B1 - A1,
        A1, 0.3,
        "$\alpha$"
    );

    picture delta_z_label;
    delta_z_label := thelabel.top("$\delta Z_{AB}$", O);

    path ext_line;

    dim_line_1 := get_dim_line(
        B1, B̄,
        0.49 * length(B̄ - B1),
        90, 90
    );
    drawdblarrow dim_line_1;

    ext_line := get_ext_line(
        point 0.5 of dim_line_1,
        angle(direction 0.5 of dim_line_1) - 90,
        "$\delta Z_{AB}$"
    );
    draw ext_line;
    unfill boundingbox delta_z_label shifted point 1.5 of ext_line;
    draw delta_z_label shifted point 1.5 of ext_line;

    dim_line_2 := get_dim_line(
        B2, B̃2,
        1.5 * length(B̃2 - B2),
        90, 90
    );
    drawdblarrow dim_line_2;
    draw delta_z_label rotated 90 shifted point 0.5 of dim_line_2;

    draw_point_circ_with_label(A1, "$A_1$", llft);
    draw_point_circ_with_label(A2, "$A_2$", ulft);

    draw_point_circ_with_label(B1, "$B_1$", urt);
    draw_point_circ_with_label(B2, "$B_2$", top);

    label.urt("$\tilde{B}_2$", B̃2);

    draw_point_circ_with_label(B̄, "$\bar{B}$", llft);
\stopuseMPgraphic
\stopcomponent