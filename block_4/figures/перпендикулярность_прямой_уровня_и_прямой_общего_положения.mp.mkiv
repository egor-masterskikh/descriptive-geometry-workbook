\startcomponent .
\startuseMPgraphic{перпендикулярность горизонтали и прямой общего положения}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := .5 * fig_width;
    y1_max := .5 * fig_height;
    z_max := fig_height - y1_max;

    draw_axes(fig_width, y1_max, z_max, 0);

    picture pic;

    pic := image(
        O := fig_width * left;

        % ПЕРЕСЕКАЮЩИЕСЯ ПРЯМЫЕ

        O := O + x_max * right;

        pair A[];

        A.x := .75 * x_max;
        A.y := .25 * y1_max;
        A.z := .2 * z_max;

        A1 := O + (-A.x, -A.y);
        A2 := O + (-A.x, A.z);

        path a[], h[];

        a1_inclination := 60;
        a2_inclination := -50;
        h1_inclination := a1_inclination - 90;

        draw A1--A2;

        pickup base_pen;

        a1 := get_line(
            0.8 * min(
                abs((x_max - A.x) / cosd a1_inclination),
                abs((y1_max - A.y) / sind a1_inclination)
            ), a1_inclination,
            A1, 1
        );
        draw a1;

        draw get_line(
            0.8 * min(
                abs((x_max - A.x) / cosd a2_inclination),
                abs((z_max - A.z) / sind a2_inclination)
            ), a2_inclination,
            A2, 1
        );

        h1 := get_line(
            0.8 * min(
                abs(A.x / cosd h1_inclination),
                abs((y1_max - A.y) / sind h1_inclination)
            ), h1_inclination,
            A1, 0
        );
        draw h1;

        draw get_line(
            0.8 * A.x, 0,
            A2, 0
        );

        pickup thin_pen;

        draw get_perp_mark(
            (point 0 of a1) - A1, (point 1 of h1) - A1,
            A1, 0.15
        );

        draw_point_circ(A1);
        draw_point_circ(A2);

        % СКРЕЩИВАЮЩИЕСЯ ПРЯМЫЕ

        O := O + x_max * right;
        
        pair K[], L[];

        K.x := .4 * x_max;
        K.y := .5 * y1_max;
        
        L.x := .6 * x_max;
        L.z := .5 * z_max;

        K1 := O + (-K.x, -K.y);
        L2 := O + (-L.x, L.z);

        path a[], h[];

        a1_inclination := -70;
        a2_inclination := -30;
        h1_inclination := a1_inclination + 90;

        pickup base_pen;

        a1 := get_line(
            0.8 * min(
                abs(x_max / cosd a1_inclination),
                abs(y1_max / sind a1_inclination)
            ), a1_inclination,
            K1, 0.5
        );
        draw a1;

        draw get_line(
            0.7 * min(
                abs(x_max / cosd a2_inclination),
                abs(z_max / sind a2_inclination)
            ), a2_inclination,
            L2, 0.5
        );

        h1 := get_line(
            0.7 * min(
                abs(x_max / cosd h1_inclination),
                abs(y1_max / sind h1_inclination)
            ), h1_inclination,
            K1, 0.5
        );
        draw h1;

        draw get_line(
            0.8 * x_max, 0,
            L2, 0.4
        );

        pickup thin_pen;

        draw get_perp_mark(
            (point 1 of a1) - K1, (point 1 of h1) - K1,
            K1, 0.2
        );
    );

    draw pic;

    i := 0;

    for item within pic:
        pickup penpart item;
        item_pen_thickness := length(rt O - lft O);

        if item_pen_thickness = base_line_thickness:
            label.
            if i = 0:
                ulft("$a_1$", point 0
            elseif i = 1:
                top("$a_2$", point 0
            elseif i = 2:
                top("$h_1$", point 1
            elseif i = 3:
                top("$h_2$", point 1
            elseif i = 4:
                rt("$a_1$", point 1
            elseif i = 5:
                top("$a_2$", point 0
            elseif i = 6:
                top("$h_1$", point 0
            elseif i = 7:
                ulft("$h_2$", point 1
            fi
            of pathpart item);

            i := i + 1;
        fi
    endfor
\stopuseMPgraphic

\startuseMPgraphic{перпендикулярность фронтали и прямой общего положения}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := .5 * fig_width;
    y1_max := .5 * fig_height;
    z_max := fig_height - y1_max;

    draw_axes(fig_width, y1_max, z_max, 0);

    pic := pic reflectedabout (O, X);  % используем предыдущий рисунок

    draw pic;

    i := 0;

    for item within pic:
        pickup penpart item;
        item_pen_thickness := length(rt O - lft O);

        if item_pen_thickness = base_line_thickness:
            label.
            if i = 0:
                top("$b_2$", point 0
            elseif i = 1:
                ulft("$b_1$", point 0
            elseif i = 2:
                top("$f_2$", point 1
            elseif i = 3:
                rt("$f_1$", point 1
            elseif i = 4:
                top("$b_2$", point 1
            elseif i = 5:
                lft("$b_1$", point 0
            elseif i = 6:
                top("$f_2$", point 0
            elseif i = 7:
                top("$f_1$", point 1
            fi
            of pathpart item);

            i := i + 1;
        fi
    endfor
\stopuseMPgraphic
\stopcomponent
