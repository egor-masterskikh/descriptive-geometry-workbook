\startcomponent .
\startuseMPgraphic{замена плоскостей проекций для прямой}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := .8 * fig_width;
    y1_max := .7 * fig_height;
    z_max := fig_height - y1_max;

    draw_axes(x_max, 0, 0, 0);

    pair Pi_label_point;

    Pi_label_point := point 0 of (O--X);
    label.llft(btex П₁ etex, Pi_label_point);
    label.ulft(btex П₂ etex, Pi_label_point);

    pair A[], B[];

    A.x := .7 * x_max;
    A.y := .13 * y1_max;
    A.z := .2 * z_max;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);

    B.x := .2 * x_max;
    B.y := .4 * y1_max;
    B.z := .75 * z_max;

    B1 := (-B.x, -B.y);
    B2 := (-B.x, B.z);

    A12 := (xpart A1, 0);
    B12 := (xpart B1, 0); 

    draw A1--A12;
    draw_marked(A12--A2, 1);

    draw B1--B12;
    draw_marked(B12--B2, 2);

    pickup base_pen;

    path l[];

    l1 := get_line_by_two_pts_and_draw_label(
        A1, B1, A.y, 2.5 * font_size,
        0.5, "$l_1$", top
    );
    draw l1;

    draw_line_by_two_pts_with_label(
        A2, B2, A.z, 2 * font_size,
        0.5, "$l_2$", ulft
    );

    pickup thin_pen;

    pair O[];
    pair X[];
    path OX[];

    X14 := B1 - A1;
    OX14 := reverse l1 shifted (unitvector X14 rotated -90 scaled (2.2 * font_size));
    O14 := point 0 of OX14;

    drawarrow OX14;
    label.top("$X_{14}$", point 1 of OX14);

    Pi_label_point := point 0 of OX14;
    label.top(btex П₁ etex, Pi_label_point);
    label.llft(btex П₄ etex, Pi_label_point);

    (A14 - A1) dotprod X14 = 0;
    A14 = O14 + whatever * X14;

    (B14 - B1) dotprod X14 = 0;
    B14 = O14 + whatever * X14;

    A4 - A14 = unitvector(A14 - A1) scaled A.z;
    B4 - B14 = unitvector(B14 - B1) scaled B.z;

    l4 := get_line_by_two_pts_and_draw_label(
        A4, B4, A.z, 2 * font_size,
        .6, "$l_4$", top
    );
    draw l4 withpen base_pen;

    draw_ext_line_with_label(.5[A4, B4], -135, "$н.в.\ AB$");

    draw_marked(A1--A14, 3);
    draw_marked(B1--B14, 3);

    draw_marked(A14--A4, 1);
    draw_marked(B14--B4, 2);

    draw_perp_mark(B4 - B1, -X14, B14);

    path alpha_ang_mark;

    alpha_ang_mark := get_angle_mark_and_draw_label(
        B4 - A4, vec (1, 0) of OX14, A4,
        0.3, "$\alpha$"
    );
    draw alpha_ang_mark;
    draw (O--(point 1 of alpha_ang_mark - A4)) scaled 1.5 shifted A4;

    A45 := (point 1 of l4) shifted (font_size * unitvector(B4 - A4));

    OX45 := get_line_and_draw_label(
        4 * font_size, angle(B4 - A4) - 90,
        A45, 0.5,
        1, "$X_{45}$", lft
    );
    drawarrow OX45;

    Pi_label_point := point 0 of OX45;
    label.ulft(btex П₄ etex, Pi_label_point);
    label.lrt(btex П₅ etex, Pi_label_point);

    X45 := vec (0, 1) of OX45;

    A5 - A45 = unitvector(A45 - A4) * length(A1 - A14);

    draw A4--A45;
    draw_marked(A45--A5, 3);

    draw_perp_mark(A4 - A45, -X45, A45);

    draw_point_circ_with_label(A1, "$A_1$", urt);
    draw_point_circ_with_label(A2, "$A_2$", ulft);
    draw_point_circ_with_label(B1, "$B_1$", urt);
    draw_point_circ_with_label(B2, "$B_2$", lrt);

    draw_point_circ_with_label(A4, "$A_4$", bot);
    draw_point_circ_with_label(B4, "$B_4$", bot);

    draw_point_circ_with_label(A5, "$B_5 \equiv (A_5) \equiv l_5$", rt);
\stopuseMPgraphic
\stopcomponent
