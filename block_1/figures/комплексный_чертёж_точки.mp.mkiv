\startcomponent .
    \startuseMPgraphic{комплексный чертёж точки}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := .618 * fig_width;    % длина оси X
    y3_max := fig_width - x_max;  % длина оси Y3

    z_max := .618 * fig_height;    % длина оси Z
    y1_max := fig_height - z_max;  % длина оси Y1

    draw_axes(x_max, y1_max, z_max, y3_max);

    pair A[];

    A.x := .7 * x_max;
    A.y := .6 * y3_max;
    A.z := .7 * z_max;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);
    A3 := (A.y, A.z);

    A12 = whatever[A1, A2] = whatever[O, X];
    A23 = whatever[A2, A3] = whatever[O, Z];

    % точка пересечения прямой, проходящей через A1 горизонтально, и постоянной прямой чертежа
    A13 = whatever[O, (y3_max, -y3_max)];
    (A13 - A1) dotprod (Y1 - O) = 0;

    A131 = whatever[A1, A13] = whatever[O, Y1];  % точка пересечения прямой A1--A13 и оси OY1
    A133 = whatever[A3, A13] = whatever[O, Y3];  % точка пересечения прямой A13--A3 и оси OY3

    draw A1--A2;
    draw A1--A13;
    draw A13--A3;
    draw A2--A3;

    for point_pos=A1, A2, A3, A12, A23:
        draw_point_circ(point_pos);
    endfor

    label.bot(btex $A_1$ etex, A1);
    label.top(btex $A_2$ etex, A2);
    label.top(btex $A_3$ etex, A3);
    label.urt(btex $A_{12}$ etex, A12);
    label.ulft(btex $A_{23}$ etex, A23);

    label.top(btex $X_A$ etex, .45[A2, A23]);
    label.top(btex $X_A$ etex, .55[A12, O]);
    label.top(btex $X_A$ etex, .45[A1, A131]);

    label.lft(btex $Z_A$ etex rotated 90, .55[A12, A2]);
    label.lft(btex $Z_A$ etex rotated 90, .55[O, A23]);
    label.lft(btex $Z_A$ etex rotated 90, .55[A133, A3]);

    label.lft(btex $Y_A$ etex rotated 90, .55[A1, A12]);
    label.lft(btex $Y_A$ etex rotated 90, .55[A131, O]);
    label.lft(btex $Y_A$ etex rotated 90, .7[A13, A133]);
    label.top(btex $Y_A$ etex, .45[A23, A3]);
    label.top(btex $Y_A$ etex, .45[O, A133]);
    label.top(btex $Y_A$ etex, .4[A131, A13]);
    \stopuseMPgraphic
\stopcomponent