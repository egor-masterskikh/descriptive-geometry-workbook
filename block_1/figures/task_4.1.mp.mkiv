\startcomponent .
    \startuseMPgraphic{block 1/task 4.1}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := fig_width;
    y1_max := .5 * fig_height;
    z_max := fig_height - y1_max;

    draw_axes(x_max, 0, 0, 0);

    pair A[];
    A.x := .5 * x_max;
    A.y := .4 * y1_max;
    A.z := .6 * z_max;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);

    path a[], b[];
    pair a[]_linc, a[]_rinc, b[]_linc, b[]_rinc;

    a1_linc := .4 * x_max * dir 30;  % приращение a1 слева от A1
    a1_rinc := a1_linc;  % приращение a1 справа от A1
    a2_linc := .4 * x_max * dir -18;  % приращение a2 слева от A2
    a2_rinc := a2_linc;  % приращение a2 справа от A2

    b1_linc := .4 * x_max * dir -18;
    b1_rinc := b1_linc;
    b2_linc := .4 * x_max * dir 40;
    b2_rinc := b2_linc;

    a1 := (A1 - a1_linc)--(A1 + a1_rinc);
    a2 := (A2 - a2_linc)--(A2 + a2_rinc);

    b1 := (A1 - b1_linc)--(A1 + b1_rinc);
    b2 := (A2 - b2_linc)--(A2 + b2_rinc);

    pickup pencircle scaled base_line_thickness;

    draw a1;
    draw a2;
    draw b1;
    draw b2;

    pickup pencircle scaled thin_line_thickness;

    draw A1--A2;
    draw_point_circ(A1);
    draw_point_circ(A2);

    label.ulft(btex $a_1$ etex, A1 - a1_linc);
    label.top(btex $b_1$ etex, A1 + b1_rinc);
    label.top(btex $a_2$ etex, A2 - a2_linc);
    label.ulft(btex $b_2$ etex, A2 + b2_rinc);
    \stopuseMPgraphic
\stopcomponent