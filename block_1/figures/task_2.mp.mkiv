\startcomponent .
    \startuseMPgraphic{block 1/task 2}
    fig_width := \framedwidth;
    fig_height := \framedheight;

    x_max := fig_width;
    z_max := .5 * fig_height;
    y1_max := fig_height - z_max;

    draw_axes(x_max, y1_max, z_max, 0);

    pair A[], B[], C[], D[], E[];
    path l[];

    A.x := .7 * x_max;
    A.y := .8 * y1_max;
    A.z := .7 * z_max;

    A1 := (-A.x, -A.y);
    A2 := (-A.x, A.z);

    C.x := .45 * x_max;
    C.y := .56 * y1_max;
    C.z := .67 * z_max;

    C1 := (-C.x, -C.y);
    C2 := (-C.x, C.z);

    E.x := .15 * x_max;
    E.y := .58 * y1_max;
    E.z := .32 * z_max;

    E1 := (-E.x, -E.y);
    E2 := (-E.x, E.z);

    l1 := 2.3[A1, C1]--1.5[C1, A1];
    l2 := 1.1[A2, E2]--1.2[E2, A2];

    B.x := .6 * x_max;
    B.z := .5 * z_max;

    xpart B1 = -B.x;
    B1 = whatever[A1, C1];
    B2 := (-B.x, B.z);

    D.x := .3 * x_max;
    D.y := .2 * y1_max;

    D1 := (-D.x, -D.y);
    xpart D2 = -D.x;
    D2 = whatever[A2, E2];


    pickup pencircle scaled base_line_thickness;

    draw l1;
    draw l2;
    
    pickup pencircle scaled thin_line_thickness;

    draw A1--A2;
    draw B1--B2;
    draw C1--C2;
    draw D1--D2;
    draw E1--E2;

    for point_pos=A1, A2, B1, B2, C1, C2, D1, D2, E1, E2:
        draw_point_circ(point_pos);
    endfor

    label.bot(btex $A_1$ etex, A1);
    label.top(btex $A_2$ etex, A2);
    label.lrt(btex $B_1$ etex, B1);
    label.lrt(btex $B_2$ etex, B2);
    label.bot(btex $C_1$ etex, C1);
    label.top(btex $C_2$ etex, C2);
    label.rt(btex $D_1$ etex, D1);
    label.urt(btex $D_2$ etex, D2);
    label.bot(btex $E_1$ etex, E1);
    label.top(btex $E_2$ etex, E2);

    label.top(btex $l_1$ etex, 1.4[C1, A1]);
    label.top(btex $l_2$ etex, 1.15[E2, A2]);
    \stopuseMPgraphic
\stopcomponent